apply plugin: 'java-platform'
apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/publishing.gradle"

// All dependencies used by the netCDF-Java library are defined here

javaPlatform {
  allowDependencies()
}

dependencies {
  // Results in upgrading jackson-databind to 2.10.5.1, which contains a fix for
  // CVE-2020-25649 (https://nvd.nist.gov/vuln/detail/CVE-2020-25649)
  // See https://github.com/FasterXML/jackson-databind/issues/2589#issuecomment-736973925
  // jackson-databind is pulled in by AWS SDK (true as of version 2.15.40), so once they update to
  // a newer version, we can get rid of this (and the javaPlatform allowDependencies() block above).
  api enforcedPlatform('com.fasterxml.jackson:jackson-bom:2.10.5.20201202')
  constraints {
    // Note: The depVersion variable is defined in gradle/any/shared-mvn-coords.gradle and is used for dependencies
    // that are used in different configurations of a build that need access to the full maven coordinates.

    // general dependencies
    api "com.google.protobuf:protobuf-java:${depVersion.protobuf}"
    api "com.google.protobuf:protoc:${depVersion.protobuf}"
    api 'com.google.guava:guava:30.1-jre'
    api 'com.google.re2j:re2j:1.3'
    api 'org.jdom:jdom2:2.0.6'
    api 'joda-time:joda-time:2.10.3' // replace by javax.time

    // netcdf4, dap4:d4lib
    // Tricky dependency here. We need to make sure that we keep in-line with the version
    // that chronicle-map uses in the TDS, or else we see bad things happen on the TDS side.
    api 'net.java.dev.jna:jna:5.4.0'

    // Annotations: Nullable
    api 'com.google.code.findbugs:jsr305:3.0.2'

    // command line parser
    api 'com.beust:jcommander:1.78'

    // cdm-grib
    api 'edu.ucar:jj2000:5.4'
    api 'org.jsoup:jsoup:1.11.2' // HTML scraper used in GRIB

    // cdm-mcidas (GEMPAK and McIDAS IOSPs)
    api 'edu.wisc.ssec:visad-mcidas-slim-ucar-ns:20200507-2'

    // cdm-vis5d (vis5d IOSP)
    api 'edu.wisc.ssec:visad:2.0-20130124'

    // cdm-s3 (S3RandomAccessFile)
    // If updating from 2.15.40, see note the note at the top of this dependencies block regarding
    // the use of jackson-databind enforcedPlatform.
    api 'software.amazon.awssdk:s3:2.15.46'
    api 'software.amazon.awssdk:apache-client:2.15.46'

    // apache httpclient
    api 'org.apache.httpcomponents:httpclient:4.5.13'
    api 'org.apache.httpcomponents:httpmime:4.5.13'
    // version of httpcore matches the one used by httpclient
    // so if updating httpclient, look in its pom for the version of http core to use
    api 'org.apache.httpcomponents:httpcore:4.4.13'
    api "org.slf4j:jcl-over-slf4j:${depVersion.slf4j}"

    // waterml xml beans
    api 'org.apache.xmlbeans:xmlbeans:3.1.0'
    api 'org.n52.sensorweb:52n-xml-waterML-v20:2.7.0'
    api 'org.n52.sensorweb:52n-xml-gml-v321:2.7.0'
    api 'org.n52.sensorweb:52n-xml-sweCommon-v20:2.7.0'
    api 'org.n52.sensorweb:52n-xml-om-v20:2.7.0'
    api 'org.n52.sensorweb:52n-xml-sampling-v20:2.7.0'

    // netcdf-java logging
    api "org.slf4j:slf4j-api:${depVersion.slf4j}"
    runtime 'ch.qos.logback:logback-classic:1.2.3'

    //////////////////////////
    // netcdf-java testing //
    //////////////////////////

    api 'junit:junit:4.13.1'
    api 'commons-io:commons-io:2.5'

    // Fluent assertions for Java
    api 'com.google.truth:truth:1.0'

    // These two are for Spock.
    api 'org.spockframework:spock-core:1.3-groovy-2.5'
    api 'org.codehaus.groovy:groovy-all:2.5.12'

    //mockito
    api 'org.mockito:mockito-core:2.28.2'

    // opendap
    api 'pl.pragmatists:JUnitParams:1.0.5'

    // cdm-test (GRIB related testing)
    api 'org.apache.commons:commons-compress:1.12'

    ///////////////////////
    // toolsUI GUI stuff //
    ///////////////////////

    // Used by NcmlEditor. Abandoned, no updates since 2013. https://sourceforge.net/projects/bounce/
    // https://sourceforge.net/p/bounce/svn/HEAD/tree/src/main/java/org/bounce/
    api 'org.bounce:bounce:0.18'

    // http://www.jfree.org/
    api 'org.jfree:jcommon:1.0.23'
    api 'org.jfree:jfreechart:1.0.19'

    // http://www.jgoodies.com/. Latest version is 1.9.0, but there is breakage when we try to upgrade.
    api 'com.jgoodies:jgoodies-forms:1.6.0'

    // LGoodDatePicker - swing calendar widget used in TdsMonitor
    api 'com.github.lgooddatepicker:LGoodDatePicker:10.3.1'

    // legacy gradle module
    // todo: remove with legacy in 6
    api 'com.amazonaws:aws-java-sdk-s3:1.11.871'

    // In Spock, allows mocking of classes (in addition to interfaces).
    // todo: remove with legacy in 6
    runtime 'cglib:cglib-nodep:3.2.4'

    // In Spock, allows mocking of classes without default constructor (together with CGLIB).
    // todo: remove with legacy in 6
    runtime 'org.objenesis:objenesis:2.4'
  }
}

publishing {
  publications {
    netcdfJavaPlatform(MavenPublication) {
      from components.javaPlatform
    }
  }
}
