apply plugin: 'java-platform'
apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/publishing.gradle"

// All dependencies used by the netCDF-Java library are defined here

dependencies {
  constraints {
    // general dependencies
    api 'com.google.protobuf:protobuf-java:3.9.1'
    api 'com.google.guava:guava:28.0-jre'
    api 'com.google.re2j:re2j:1.3'
    api 'org.jdom:jdom2:2.0.6'
    api 'joda-time:joda-time:2.10.3' // replace by javax.time

    // see https://github.com/google/auto/blob/master/value/userguide/index.md
    api "com.google.auto.value:auto-value-annotations:1.7.4"

    // netcdf4, dap4:d4lib
    // Tricky dependency here. We need to make sure that we keep in-line with the version
    // that chronicle-map uses in the TDS, or else we see bad things happen on the TDS side.
    api 'net.java.dev.jna:jna:5.4.0'

    // Annotations: Nullable
    api 'com.google.code.findbugs:jsr305:3.0.2'

    // command line parser
    api 'com.beust:jcommander:1.78'

    // cdm-grib
    api 'edu.ucar:jj2000:5.4'
    api 'org.jsoup:jsoup:1.11.2' // HTML scraper used in GRIB

    // cdm-mcidas (GEMPAK and McIDAS IOSPs)
    api 'edu.wisc.ssec:visad-mcidas-slim-ucar-ns:20200507-2'

    // cdm-vis5d (vis5d IOSP)
    api 'edu.wisc.ssec:visad:2.0-20130124'

    // cdm-s3 (S3RandomAccessFile)
    api 'software.amazon.awssdk:s3:2.13.52'
    api 'software.amazon.awssdk:apache-client:2.13.52'

    // apache httpclient
    api 'org.apache.httpcomponents:httpclient:4.5.12'
    api 'org.apache.httpcomponents:httpmime:4.5.12'
    api "org.slf4j:jcl-over-slf4j:${depVersion.slf4j}"

    // waterml xml beans
    api 'org.apache.xmlbeans:xmlbeans:2.6.0'
    api 'org.n52.sensorweb:52n-xml-waterML-v20:2.0.0'
    api 'org.n52.sensorweb:52n-xml-gml-v321:2.0.0'
    api 'org.n52.sensorweb:52n-xml-sweCommon-v20:2.0.0'
    api 'org.n52.sensorweb:52n-xml-om-v20:2.0.0'
    api 'org.n52.sensorweb:52n-xml-sampling-v20:2.0.0'

    // netcdf-java logging
    api "org.slf4j:slf4j-api:${depVersion.slf4j}"
    runtime 'ch.qos.logback:logback-classic:1.2.3'

    //////////////////////////
    // netcdf-java testing //
    //////////////////////////

    api 'junit:junit:4.12'
    api 'commons-io:commons-io:2.5'

    // Fluent assertions for Java
    api 'com.google.truth:truth:1.0'

    // These two are for Spock.
    api 'org.spockframework:spock-core:1.3-groovy-2.5'
    api 'org.codehaus.groovy:groovy-all:2.5.12'

    //mockito
    api 'org.mockito:mockito-core:2.28.2'

    // opendap
    api 'pl.pragmatists:JUnitParams:1.0.5'

    // cdm-test (GRIB related testing)
    api 'org.apache.commons:commons-compress:1.12'

    ///////////////////////
    // toolsUI GUI stuff //
    ///////////////////////

    // Used by NcmlEditor. Abandoned, no updates since 2013. https://sourceforge.net/projects/bounce/
    // https://sourceforge.net/p/bounce/svn/HEAD/tree/src/main/java/org/bounce/
    api 'org.bounce:bounce:0.18'

    // http://www.jfree.org/
    api 'org.jfree:jcommon:1.0.23'
    api 'org.jfree:jfreechart:1.0.19'

    // http://www.jgoodies.com/. Latest version is 1.9.0, but there is breakage when we try to upgrade.
    api 'com.jgoodies:jgoodies-forms:1.6.0'

    // LGoodDatePicker - swing calendar widget used in TdsMonitor
    api 'com.github.lgooddatepicker:LGoodDatePicker:10.3.1'
  }
}

publishing {
  publications {
    netcdfJavaPlatform(MavenPublication) {
      from components.javaPlatform
    }
  }
}
