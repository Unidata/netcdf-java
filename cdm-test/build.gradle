description = 'Classes for CDM unit and integration testing. Relies on having access to ' +
    'cdmUnitTest files, which can be obtained at https://github.com/unidata/thredds-test-data.'
ext.title = 'Extended CDM Testing'

apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/test-only-projects.gradle"

// cdm-test is not published

dependencies {
  testImplementation enforcedPlatform(project(':netcdf-java-platform'))
  testImplementation enforcedPlatform(project(':netcdf-java-testing-platform'))

  testImplementation project(':cdm-core')
  testImplementation project(':cdm-s3')
  testImplementation project(':bufr')
  testImplementation project(':grib')
  testImplementation project(':netcdf4')
  testImplementation project(':cdm-test-utils')

  testImplementation 'commons-io:commons-io'
  testImplementation 'org.apache.commons:commons-compress'
  testImplementation 'edu.ucar:jj2000'
  testImplementation 'org.jdom:jdom2'
  testImplementation 'com.google.guava:guava'
  testImplementation 'software.amazon.awssdk:s3'
  testImplementation 'com.google.truth:truth'
  testImplementation 'org.slf4j:slf4j-api'

  testRuntimeOnly 'ch.qos.logback:logback-classic'
}

tasks.register("testIndexCreation", Test) {
  group 'Verification'
  it.filter {
    includeTestsMatching 'ucar.nc2.grib.TestGribIndexCreation'
  }
  it.dependsOn classes, testClasses
}

test {
  dependsOn 'testIndexCreation'

  // In addition to preventing TestGribIndexCreation from running during cdm-test:test,
  // this statement also excludes the results of TestGribIndexCreation from appearing in the cdm-test report:
  // "cdm-test/build/reports/tests/index.html". It's not easy to add them back in. Fortunately, those
  // results will be included in the allTests aggregate report: "build/reports/allTests/index.html".
  // They should also still get picked up by Jenkins.
  filter {
    excludeTestsMatching 'ucar.nc2.grib.TestGribIndexCreation'
  }
}
