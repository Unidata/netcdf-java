plugins {
  id "application"
}

description = 'Provides a graphical interface to the CDM library.'
ext.title = 'ToolsUI'

apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/java-library.gradle"

dependencies {
  compile enforcedPlatform(project(':netcdf-java-platform'))
  testImplementation enforcedPlatform(project(':netcdf-java-testing-platform'))

  // todo: a lot of these could probably be runtimeOnly
  compile project(':cdm:cdm-core')
  compile project(':cdm:cdm-image')    // things that depend on java.awt
  compile project(':cdm:cdm-misc')     // misc iosps
  compile project(':cdm:cdm-radial')   // radial data
  compile project(':bufr')
  compile project(':grib')
  compile project(':httpservices')
  compile project(':netcdf4')
  compile project(':opendap')
  compile project(':dap4')
  compile project(':uibase')
  compile project(':visad:cdm-mcidas') // For Gempak IOSPs.
  compile project(':waterml')

  // constrained by netcdf-java-platform
  compile 'org.jdom:jdom2'
  compile 'org.apache.httpcomponents:httpclient'
  compile 'com.google.re2j:re2j'
  compile 'org.slf4j:slf4j-api'

  testImplementation project(':cdm-test-utils')
  testImplementation 'junit:junit'

  runtimeOnly project(':cdm:cdm-s3')

  // constrained by netcdf-java-platform
  runtimeOnly 'ch.qos.logback:logback-classic'
}

application {
  mainClass.set("ucar.nc2.ui.ToolsUI")
}

jar.manifest.attributes 'Main-Class': 'ucar.nc2.ui.ToolsUI'
