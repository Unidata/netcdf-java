plugins {
  id "application"
}

description = 'Provides a graphical interface to the CDM library.'
ext.title = 'ToolsUI'

apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/java-library.gradle"

dependencies {
  implementation enforcedPlatform(project(':netcdf-java-platform'))
  testImplementation enforcedPlatform(project(':netcdf-java-testing-platform'))

  // todo: a lot of these could probably be runtimeOnly
  implementation project(':cdm:cdm-core')
  implementation project(':cdm:cdm-image')    // things that depend on java.awt
  implementation project(':cdm:cdm-misc')     // misc iosps
  implementation project(':cdm:cdm-radial')   // radial data
  implementation project(':bufr')
  implementation project(':grib')
  implementation project(':httpservices')
  implementation project(':netcdf4')
  implementation project(':opendap')
  implementation project(':dap4')
  implementation project(':udunits')
  implementation project(':uibase')
  implementation project(':visad:cdm-mcidas') // For Gempak IOSPs.
  implementation project(':waterml')

  // constrained by netcdf-java-platform
  implementation 'org.apache.xmlbeans:xmlbeans'
  implementation 'org.jdom:jdom2'
  implementation 'org.apache.httpcomponents:httpclient'
  implementation 'com.google.protobuf:protobuf-java'
  implementation 'com.google.re2j:re2j'
  implementation 'org.slf4j:slf4j-api'

  testImplementation project(':cdm-test-utils')
  testImplementation 'junit:junit'

  runtimeOnly project(':cdm:cdm-s3')

  // constrained by netcdf-java-platform
  runtimeOnly 'ch.qos.logback:logback-classic'
}

application {
  mainClass.set("ucar.nc2.ui.ToolsUI")
}

jar.manifest.attributes 'Main-Class': 'ucar.nc2.ui.ToolsUI'
