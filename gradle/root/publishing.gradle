if (name != 'netcdf-java') {
  throw new GradleException("This script plugin should only be applied to the root project, not '$name'.")
}

apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: "$rootDir/gradle/any/properties.gradle"  // For Nexus credential properties.

import org.gradle.api.internal.artifacts.publish.ArchivePublishArtifact

publishing {
  // Publish all artifacts we've added to the "archives" configuration of the root project. See fatJars.gradle.
  publications {
    rootProject.configurations.archives.allArtifacts.each {
      if (it instanceof ArchivePublishArtifact) {
        AbstractArchiveTask task = (it as ArchivePublishArtifact).archiveTask
        "$task.name"(MavenPublication) {
          artifactId task.archiveBaseName.get()
          artifact task
        }
      }
    }
  }
}

tasks.withType(GenerateModuleMetadata) {
  enabled = false
}

// The "publish" tasks require credentials for our Nexus server, which they look for in Gradle properties.
// If those properties (i.e. NEXUS_USERNAME_KEY and NEXUS_PASSWORD_KEY) haven't been provided, the build will fail.
// Therefore, we only want to configure credentials when a "publish" task is part of the execution plan. Otherwise,
// unavailable credentials could cause a build to fail even if we aren't doing any publishing. The TaskExecutionGraph
// allows us to do that.
gradle.taskGraph.whenReady {TaskExecutionGraph taskGraph ->
  // This won't find any publishToMavenLocal tasks. Those are of type PublishToMavenLocal
  Collection<Task> mavenPublishTasks = taskGraph.allTasks.findAll {
    it instanceof PublishToMavenRepository
  }

  mavenPublishTasks.each {
    it.repository.credentials.with {
      username = getPropertyOrFailBuild NEXUS_USERNAME_KEY
      password = getPropertyOrFailBuild NEXUS_PASSWORD_KEY
    }
  }
}
